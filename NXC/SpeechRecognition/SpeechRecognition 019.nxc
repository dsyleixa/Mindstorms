
// "speech" "recognition"
string version="0.18";

// programming language: NXC for Mindstorms NXT
// (c) HaWe 2011
// mail to: HaWe(at)SiriusCyberneticCorporation.org

// speak, and watch the real-time oscillograph.
// if the oscillograph appears the 2nd time you may press BtnCenter to store.

///////////////////////////////////////////////////////////////////////////////
#define printf1( _x, _y, _format1, _value1) { \
  string sval1 = FormatNum(_format1, _value1); \
  TextOut(_x, _y, sval1); \
}

#define printf2( _x, _y, _format1, _format2, _value1, _value2) { \
  string sval1 = FormatNum(_format1, _value1); \
  string sval2 = FormatNum(_format2, _value2); \
  string s =sval1+sval2; \
  TextOut(_x, _y, s); \
}

///////////////////////////////////////////////////////////////////////////////

inline bool btnhit(){
   return ( ButtonPressed(BTN1, false) || ButtonPressed(BTN2, false)
         || ButtonPressed(BTN3, false) || ButtonPressed(BTN4, false));
}


char LCDline[]={56,48,40,32,24,16,8,0};

///////////////////////////////////////////////////////////////////////////////

struct Note
{
  unsigned int Frequency;
  unsigned int Duration;
};

//*****************************************

Note ChordUp[] = {TONE_C4, 50, TONE_E4, 50, TONE_G4, 50,
  TONE_C5, 50, TONE_E5, 50, TONE_G5, 50, TONE_C6, 200};
Note ChordDn[] = {TONE_C6, 50, TONE_G5, 50, TONE_E5, 50,
  TONE_C5, 50, TONE_G4, 50, TONE_E4, 50,  TONE_C4, 200};
Note Chord[] = {TONE_C4, 50, TONE_E4, 50, TONE_G4, 50, TONE_C5, 50};
Note Beep[] = {TONE_C5, 200};
Note BeepBeep[] = {TONE_C5, 200 , 0, 100, TONE_C5, 200};
Note Blip[] = {TONE_C7, 10 };
Note BlipBlip[] = {TONE_C7, 10, 0, 20, TONE_C7, 10 };
Note Buzz[] = {220, 200 };
Note sdError[] = {TONE_C4, 50, 0, 50, TONE_C4, 50, 0, 50, TONE_C4, 50};

///////////////////////////////////////////////////////////////////////////////

#define MinPause  80
#define dbOffset  30
#define MaxRecLen 512
#define RecSlots  16

int  SoundLevel, BGrndNoise=50, dbMax;
int  SoundRecord[MaxRecLen];             // detection: 0...MaxRecLen:
byte SoundPattern[MaxRecLen][RecSlots];  // patterns
char key;

//**************************************************************************
// File I/O
//**************************************************************************

string sFileName = "FILE001.dat";

void SoundSave() {

  unsigned int nFileSize = 16+(MaxRecLen*2); // nFileSize = number of integer*2
  byte fHandle;
  int IOresult, counter=0;
  int ibuf, i;
  
  ClearScreen();
  TextOut( 0,48, "SpeechRecogn "+version);
  PlayTones(Buzz);

  DeleteFile(sFileName);
  IOresult = CreateFile(sFileName, nFileSize, fHandle);
  if (IOresult == LDR_SUCCESS) {
    ClearScreen();
    for (i=0; i<512; i++)   {
        counter+=1;
        ibuf= SoundRecord[i];
        printf1( 0, 24, "counter =%5d", counter);
        printf1( 0, 16, "loudness=%5d", ibuf);

        WriteLn(fHandle, ibuf);
    }
    CloseFile(fHandle);
    PlayTones(ChordUp);
  }
  else
  PlayTones(sdError);
  
  while(!btnhit());
  ClearScreen();
}

//*****************************************

void SoundRecall() {

  unsigned int nFileSize;
  byte fHandle, i;
  int IOresult, counter=0;;
  int ibuf;

  ClearScreen();
  TextOut( 0,48, "SpeechRecogn "+version);
  
  IOresult = OpenFileRead(sFileName, nFileSize, fHandle);
  if (IOresult == LDR_SUCCESS) {

    for (i=0; i<MaxRecLen; i++)   {

        counter+=1;
        ReadLn (fHandle, ibuf);
        SoundRecord[i]=ibuf;

        printf1( 0, 24, "counter =%5d", counter);
        printf1( 0, 16, "loudness=%5d", ibuf);
    }

    CloseFile(fHandle);
    PlayTones(ChordUp);
  }
  else
  PlayTones(sdError);
  Wait(1000);
}


///////////////////////////////////////////////////////////////////////////////


task SoundCheck() {
  int t,       // timer counter
      p,       // pause counter
      i, j,    // index
      peak,    // for oscilligraph
      slot=1,  // slot for sound records
      j_best,  // slot which matchs best
      d2,
      buf;
      
  long sum[MaxRecLen], sum_min;
  string str;

  while(true) {
    str=".";
    SoundLevel=SensorValue(S2);
    printf1(0,LCDline[0], "%s", "Loudness=");
    printf1(60,LCDline[0], "%4d", SoundLevel);

    ////////////////////////////////////////
    //  record if min sound level reached //
    ////////////////////////////////////////
    if (SoundLevel>(BGrndNoise + dbOffset)) {
      p=dbMax=0;
      t=1;
      key=-1;

      ClearScreen();
      ArrayInit (SoundRecord, 0, MaxRecLen);
      while (t<MaxRecLen) {
        SoundRecord[t]=SoundLevel=SensorValue(S2);

        //printf1(70,LCDline[0], "%4d", SoundLevel);
        if (SoundLevel > dbMax) dbMax=SoundLevel;

        // if observe Pause then Rec_Stop
        p= (SoundLevel<dbOffset)? p+1 : 0;
        if (p==MinPause) break;

        // printf1(0, LCDline[7], "%s", str);
        if (t%3==0) {
          peak=(SoundRecord[t]);
          LineOut ((t+1)/3, 0, (t+1)/3, peak/8);
        }
        
        Wait(3);
        t++;
        if(t%25==0) str+=".";
      }
      SoundRecord[0]=t-MinPause; // store current recording duration

      printf1(0, LCDline[7], "%s", "store: press BtnCtr" );
      if (t<100) Wait(200);


      //..........................
      if (btnhit()) key=getchar();
      //..........................

      //////////////////////////////////////////
      //   normalize to sound level steps     //
      //////////////////////////////////////////
      for (i=1; i<MaxRecLen; i++) {

        buf=SoundRecord[i];
        SoundRecord[i]=(buf*127/dbMax);
        if (buf < BGrndNoise)
          SoundPattern[i][0]=0;
        else
          SoundPattern[i][0]=(SoundRecord[i]/8)*8;
      }

      ClearScreen();
      for ( i=1; i<MaxRecLen-3; i+=3) {
         peak=(SoundPattern[i][0] );
         LineOut ((i+1)/3, 0, (i+1)/3, peak/2);
      }
         //..........................
         if (btnhit()) key=getchar();
         //..........................

      ClearScreen();
      SoundPattern[0][0]=SoundRecord[0]; // store current recording duration
      SoundSave();

      //////////////////////////////////////////
      //   allow sound record to be stored    //
      //////////////////////////////////////////
      //..........................
      if (btnhit()) key=getchar();
      //..........................
      ClearScreen();

      if ( key == BTNCENTER ) {
         key=-1;
         printf1(0, LCDline[1], "%s", "store in slot:");
         printf1(0, LCDline[7], "%s", "<<-1 OK/esc +1>>");
         while (key!=BTNEXIT) {
           printf1(0, LCDline[2], "%2d", slot);
           key=getchar();

           if (key==BTNLEFT) {
             if (slot>1) slot--;
             else
             if (slot==1) slot=RecSlots-1;
           }
           else
           if (key==BTNRIGHT) {
             if (slot<RecSlots-1) slot++;
             else
             if (slot==RecSlots-1) slot=1;
           }
           else
           if (key==BTNCENTER) {
             for (i=0; i<MaxRecLen; i++) {
               SoundPattern[i][slot]=SoundPattern[i][0];
             }
             printf1(1, LCDline[2], "stored: Slot %2d", slot );
             Wait(300);
             key=BTNEXIT ;
           }
         } // while
         ClearScreen();
         key=-1;
      }
      
      else

      //////////////////////////////////////////
      //  compare and recognize sound record  //
      //////////////////////////////////////////

      {
        sum_min=LONG_MAX; j_best=INT_MAX;
        str=".";
        for (j=1; j<RecSlots; j++) {
          TextOut(0, LCDline[1], str);
          sum[j]=0;
          if (SoundPattern[0][j]>10) {
            for (i=1; i < MaxRecLen; i++) {
              d2=(SoundPattern[i][0]-SoundPattern[i][j]);
              d2=d2*d2;
              sum[j]=sum[j] + d2;
            }
          }
          else {
            sum[j]=LONG_MAX;
          }
          buf=sum[j];

          if (sum[j]<sum_min) {
            sum_min=sum[j];
            j_best=j;

          }
          str+=".";
        } // for j

        ClearLine(LCDline[1]);
        if (j_best< INT_MAX)
          printf1(0, LCDline[1], "recogn=Pattern%2d", j_best );

      } // else compare
    }

  } // while
} // SoundCheck

task main(){

  SetLongAbort(true);
  SetSensorType(S2, SENSOR_TYPE_SOUND_DBA);
  SetSensorMode(S2, SENSOR_MODE_RAW);

  Wait(500);
  BGrndNoise=SensorValue(S2)+18;

  StartTask(SoundCheck);
  while (true)  {


  }
}


